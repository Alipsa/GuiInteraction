plugins {
    id 'groovy'
    id 'signing'
    id 'maven-publish'
    id("se.alipsa.nexus-release-plugin") version '2.0.1'
    id 'se.alipsa.gi.fatjar-conventions'
}

description = 'Allows Gade Gui Interactive capabilities from a standalone console app'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

java {
    withSourcesJar()
    withJavadocJar()
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
    if (version.contains('SNAPSHOT')) {
        // Slows down the build (a lot), use only if local SNAPSHOTS are needed
        mavenLocal()
    }
}

dependencies {
    implementation project(':gi-common')
    implementation libs.jsoup
    compileOnly libs.groovy
    testImplementation libs.groovy
    testImplementation platform(libs.junit.bom)
    testImplementation libs.junit.jupiter
    testRuntimeOnly libs.junit.platform.launcher
}

test {
    useJUnitPlatform()
}

tasks.named('javadocJar', Jar) {
    from tasks.named('groovydoc')
    dependsOn tasks.named('groovydoc')
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact(tasks.named('fatJar'))
            pom {
                name = 'Gade standalone UI interaction library for the console'
                description = "${project.description}"
                url = "https://github.com/Alipsa/GuiInteraction/gi-console"
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/Alipsa/GuiInteraction/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'perNyfelt'
                        name = 'Per Nyfelt'
                    }
                }
                scm {
                    url = 'https://github.com/Alipsa/GuiInteraction/tree/main'
                    connection = 'scm:git:https://github.com/Alipsa/GuiInteraction.git'
                    developerConnection = 'scm:git:https://github.com/Alipsa/GuiInteraction.git'
                }
            }
        }
    }
}

signing {
    if (project.properties['signing.keyId'] != null) {
        sign publishing.publications.maven
    } else {
        project.logger.lifecycle("signing.keyId is not defined, skipping signing of artifacts...")
    }
}

nexusReleasePlugin {
    userName = project.ext.properties.sonatypeUsername
    password = project.ext.properties.sonatypePassword
    mavenPublication = publishing.publications.maven
}
