plugins {
    id 'groovy'
    id 'maven-publish'
}

group = 'se.alipsa.gi'
version = '1.0.0-SNAPSHOT'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
    if (version.contains('SNAPSHOT')) {
        // Slows down the build (a lot), use only if local SNAPSHOTS are needed
        mavenLocal()
    }
}

dependencies {
    def groovyVersion = '4.0.26'

    implementation project(':gi-common')
    compileOnly "org.apache.groovy:groovy:${groovyVersion}"
    testImplementation platform('org.junit:junit-bom:5.10.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

def fatJarContainer = tasks.register('fatJar', Jar) {
    dependsOn(classes)
    archiveClassifier = 'fatjar'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    with jar
}


jar {
    dependsOn fatJarContainer
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifacts = [jar, fatJarContainer, javadocJar, sourcesJar]
            pom {
                name = 'Gade standalone UI interaction library for the console'
                description = "${project.description}"
                url = "https://github.com/Alipsa/GuiInteraction/gi-console"
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/Alipsa/GuiInteraction/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'perNyfelt'
                        name = 'Per Nyfelt'
                    }
                }
                scm {
                    url = 'https://github.com/Alipsa/GuiInteraction/tree/main'
                    connection = 'scm:git:https://github.com/Alipsa/GuiInteraction.git'
                    developerConnection = 'scm:git:https://github.com/Alipsa/GuiInteraction.git'
                }
            }
        }
    }
}