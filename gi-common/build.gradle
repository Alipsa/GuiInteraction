plugins {
    id 'groovy'
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id("se.alipsa.nexus-release-plugin") version '2.0.1'
}

description = 'Common code for Gade Gui Interactive capabilities'

java {
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
    if (version.contains('SNAPSHOT')) {
        // Slows down the build (a lot), use only if local SNAPSHOTS are needed
        mavenLocal()
    }
}

dependencies {
    compileOnly libs.groovy

    // Tika for MIME type detection - using core only for lightweight footprint
    // tika-core provides detection via file extension and magic bytes (~1MB vs ~50MB for full parsers)
    implementation libs.tika.core

    api platform(libs.matrix.bom)
    api libs.matrix.core
    api libs.matrix.charts

    api libs.commonmark
    implementation libs.commonmark.ext.gfm.tables

    testImplementation platform(libs.junit.bom)
    testImplementation libs.junit.jupiter
    testRuntimeOnly libs.junit.platform.launcher
    testImplementation libs.groovy
}

test {
    useJUnitPlatform()
}

tasks.named('javadocJar', Jar) {
    from tasks.named('groovydoc')
    dependsOn tasks.named('groovydoc')
}

jar {
    dependsOn classes
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = 'Common code for Gade standalone GUI interaction library'
                description = "${project.description}"
                url = "https://github.com/Alipsa/GuiInteraction"
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/Alipsa/GuiInteraction/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'perNyfelt'
                        name = 'Per Nyfelt'
                    }
                }
                scm {
                    url = 'https://github.com/Alipsa/GuiInteraction/tree/main'
                    connection = 'scm:git:https://github.com/Alipsa/GuiInteraction.git'
                    developerConnection = 'scm:git:https://github.com/Alipsa/GuiInteraction.git'
                }
            }
        }
    }
}

signing {
    if (project.properties['signing.keyId'] != null) {
        sign publishing.publications.maven
    } else {
        project.logger.lifecycle("signing.keyId is not defined, skipping signing of artifacts...")
    }
}

nexusReleasePlugin {
    userName = project.ext.properties.sonatypeUsername
    password = project.ext.properties.sonatypePassword
    mavenPublication = publishing.publications.maven
}
