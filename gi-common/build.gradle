plugins {
    id 'groovy'
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id("se.alipsa.nexus-release-plugin") version '2.0.0'
}

description = 'Common code for Gade Gui Interactive capabilities'

java {
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
    if (version.contains('SNAPSHOT')) {
        // Slows down the build (a lot), use only if local SNAPSHOTS are needed
        mavenLocal()
    }
}

dependencies {
    def tikaVersion = '3.2.3'

    compileOnly 'org.apache.groovy:groovy:5.0.3'

    // Tika for MIME type detection - using core only for lightweight footprint
    // tika-core provides detection via file extension and magic bytes (~1MB vs ~50MB for full parsers)
    implementation "org.apache.tika:tika-core:${tikaVersion}"

    api (platform("se.alipsa.matrix:matrix-bom:2.3.0"))
    api 'se.alipsa.matrix:matrix-core'
    api 'se.alipsa.matrix:matrix-charts'

    api "org.commonmark:commonmark:0.27.0"
    implementation "org.commonmark:commonmark-ext-gfm-tables:0.27.0"

    // Logging - SLF4J API (implementation provided by Tika's transitive deps or user's choice)
    api 'org.slf4j:slf4j-api:2.0.17'

    testImplementation platform('org.junit:junit-bom:6.0.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.apache.groovy:groovy:5.0.3'
}

test {
    useJUnitPlatform()
}

tasks.named('javadocJar', Jar) {
    from tasks.named('groovydoc')
    dependsOn tasks.named('groovydoc')
}

jar {
    dependsOn classes
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = 'Common code for Gade standalone GUI interaction library'
                description = "${project.description}"
                url = "https://github.com/Alipsa/GuiInteraction"
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/Alipsa/GuiInteraction/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'perNyfelt'
                        name = 'Per Nyfelt'
                    }
                }
                scm {
                    url = 'https://github.com/Alipsa/GuiInteraction/tree/main'
                    connection = 'scm:git:https://github.com/Alipsa/GuiInteraction.git'
                    developerConnection = 'scm:git:https://github.com/Alipsa/GuiInteraction.git'
                }
            }
        }
    }
}

signing {
    if (project.properties['signing.keyId'] != null) {
        sign publishing.publications.maven
    } else {
        project.logger.lifecycle("signing.keyId is not defined, skipping signing of artifacts...")
    }
}

nexusReleasePlugin {
    userName = project.ext.properties.sonatypeUsername
    password = project.ext.properties.sonatypePassword
    mavenPublication = publishing.publications.maven
}