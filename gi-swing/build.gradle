plugins {
    //id 'java'
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id("se.alipsa.nexus-release-plugin") version '2.0.1'
    id 'se.alipsa.gi.fatjar-conventions'
}

description = 'Allows Gade Gui Interactive capabilities from a standalone app'

java {
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
  options.compilerArgs << '-parameters'
}

tasks.named('javadocJar', Jar) {
  from tasks.named('groovydoc')
  dependsOn tasks.named('groovydoc')
}

dependencies {
  org.gradle.internal.os.OperatingSystem os = org.gradle.internal.os.OperatingSystem.current()

  // check https://repo1.maven.org/maven2/org/openjfx/javafx/20/javafx-20.pom for the platform property
  def qualifier = 'unknown'
  if (os.isLinux()) {
    qualifier='linux'
  } else if (os.isWindows()) {
    qualifier = 'win'
  } else if (os.isMacOsX()) {
    qualifier = 'mac-aarch64'
  }
  def javaFxVersion = libs.versions.javafx.get()

  implementation project(':gi-common')
  compileOnly libs.groovy.all
  implementation libs.swing.yearmonth.picker

  implementation libs.lgooddatepicker

  // JavaFX is needed at compile-time only for matrix-charts type resolution
  // The actual JavaFX Chart types are referenced by matrix-charts but not used in gi-swing
  compileOnly "org.openjfx:javafx-graphics:${javaFxVersion}:$qualifier"
  compileOnly "org.openjfx:javafx-base:${javaFxVersion}:$qualifier"
  compileOnly "org.openjfx:javafx-controls:${javaFxVersion}:$qualifier"

  // SVG rendering is handled by matrix-charts SvgPanel
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
    if (version.contains('SNAPSHOT')) {
        // Slows down the build (a lot), use only if local SNAPSHOTS are needed
        mavenLocal()
    }
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      artifact(tasks.named('fatJar'))
      pom {
        name = 'Gade standalone GUI interaction library for Swing'
        description = "${project.description}"
        url = "https://github.com/Alipsa/GuiInteraction/gi-swing"
        licenses {
          license {
            name = 'MIT License'
            url = 'https://raw.githubusercontent.com/Alipsa/GuiInteraction/main/LICENSE'
          }
        }
        developers {
          developer {
            id = 'perNyfelt'
            name = 'Per Nyfelt'
          }
        }
        scm {
          url = 'https://github.com/Alipsa/GuiInteraction/tree/main'
          connection = 'scm:git:https://github.com/Alipsa/GuiInteraction.git'
          developerConnection = 'scm:git:https://github.com/Alipsa/GuiInteraction.git'
        }
      }
    }
  }
}

signing {
  if (project.properties['signing.keyId'] != null) {
    sign publishing.publications.maven
  } else {
    project.logger.lifecycle("signing.keyId is not defined, skipping signing of artifacts...")
  }
}

nexusReleasePlugin {
  userName = project.ext.properties.sonatypeUsername
  password = project.ext.properties.sonatypePassword
  mavenPublication = publishing.publications.maven
}
