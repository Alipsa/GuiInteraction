plugins {
    //id 'java'
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id("se.alipsa.nexus-release-plugin") version '2.0.0'
}

group = 'se.alipsa.gi'
version = '0.1.0'
description = 'Allows Gade Gui Interactive capabilities from a standalone app'

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
  options.compilerArgs << '-parameters'
}

dependencies {

  implementation project(':gi-common')
  compileOnly 'org.apache.groovy:groovy-all:5.0.3'
  implementation "se.alipsa:swing-yearmonth-picker:1.0.0"

  implementation 'com.github.lgooddatepicker:LGoodDatePicker:11.2.1'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
    if (version.contains('SNAPSHOT')) {
        // Slows down the build (a lot), use only if local SNAPSHOTS are needed
        mavenLocal()
    }
}

def fatJarContainer = tasks.register('fatJar', Jar) {
  dependsOn(classes)
  archiveClassifier = 'fatjar'
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  from {
    configurations.runtimeClasspath.collect {
      it.isDirectory() ? it : zipTree(it)
    }
  }
  exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
  with jar
}


jar {
  dependsOn fatJarContainer
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact(fatJarContainer)
            pom {
                name = 'Gade standalone GUI interaction library for Swing'
                description = "${project.description}"
                url = "https://github.com/Alipsa/GuiInteraction/gi-swing"
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/Alipsa/GuiInteraction/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'perNyfelt'
                        name = 'Per Nyfelt'
                    }
                }
                scm {
                    url = 'https://github.com/Alipsa/GuiInteraction/tree/main'
                    connection = 'scm:git:https://github.com/Alipsa/GuiInteraction.git'
                    developerConnection = 'scm:git:https://github.com/Alipsa/GuiInteraction.git'
                }
            }
        }
    }
}

signing {
  if (project.properties['signing.keyId'] != null) {
    sign publishing.publications.maven
  } else {
    project.logger.lifecycle("signing.keyId is not defined, skipping signing of artifacts...")
  }
}

nexusReleasePlugin {
  userName = project.ext.properties.sonatypeUsername
  password = project.ext.properties.sonatypePassword
  mavenPublication = publishing.publications.maven
}