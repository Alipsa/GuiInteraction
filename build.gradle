plugins {
    //id 'java'
    id 'groovy'
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id('io.github.gradle-nexus.publish-plugin') version '2.0.0'
    id "com.github.ben-manes.versions" version "0.53.0"
    id "org.owasp.dependencycheck" version "12.1.0"
}

group = 'se.alipsa.gi'
version = '0.3.0-SNAPSHOT'
description = 'Allows Gade Gui Interactive capabilities from a standalone app'


java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

subprojects {
    group = rootProject.group
    version = rootProject.version
    if (it.plugins.hasPlugin('java')) {
        java {
            toolchain {
                languageVersion.set(JavaLanguageVersion.of(21))
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

nexusPublishing {
    repositories {
        sonatype() // automatically resolves to sonatype's ossrh for release and snapshot repositories
    }
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

// Extract leading numeric major: "5.0.0", "v5.0.0", "5.0.0-RC1"
def majorOf = { String v ->
    def m = (v ?: "") =~ /(?i)^\D*?(\d+)/
    m.find() ? (m.group(1) as int) : -1
}

// https://github.com/ben-manes/gradle-versions-plugin
tasks.named("dependencyUpdates").configure {
    gradleReleaseChannel = "release"
    resolutionStrategy {
        componentSelection {
            all { s ->
                if (isNonStable(s.candidate.version) && !isNonStable(s.currentVersion)) {
                    s.reject('Release candidate')
                }
                if (s.candidate.group == 'org.openjfx' && majorOf(s.candidate.version) >= 24) {
                    //println "## Rejecting JavaFX version ${s.candidate}"
                    s.reject('Blocked: Javafx >= 24 (needs Java 25)')
                }
            }
        }
    }
}

// OWASP Dependency Check configuration
// Run with: ./gradlew dependencyCheckAnalyze
// Reports are generated in build/reports/dependency-check-report.html
dependencyCheck {
    // Scan all configurations including test dependencies
    scanConfigurations = ['runtimeClasspath', 'testRuntimeClasspath']
    // Fail the build if a CVSS score of 7 or higher is found (HIGH/CRITICAL)
    failBuildOnCVSS = 7.0f
    // Output formats: HTML, JSON, CSV, XML, SARIF, JUNIT, ALL
    formats = ['HTML', 'JSON']
    // Suppress false positives (create suppressions.xml if needed)
    suppressionFile = file("${rootDir}/config/owasp-suppressions.xml").exists() ?
            "${rootDir}/config/owasp-suppressions.xml" : null
}